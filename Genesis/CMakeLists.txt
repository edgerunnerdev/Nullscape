cmake_minimum_required(VERSION 3.15)
project(Genesis)

include(ExternalLibraries)

set(FORGE_PROCESS_PORT 47563)
set(FORGE_LISTENER_PORT 47564)

add_subdirectory("libs/GenesisCore")
add_subdirectory("libs/GenesisResComp")
add_subdirectory("tools")

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/cmake)

file(GLOB_RECURSE SOURCE_FILES CONFIGURE_DEPENDS src/*.cpp src/*.h src/*.hpp)
source_group(TREE ${CMAKE_CURRENT_LIST_DIR}/src FILES ${SOURCE_FILES})
list(APPEND SOURCE_FILES tools/modelcomp/src/modelserialization.hpp)

find_package(bullet3 REQUIRED)
find_package(CmdParser REQUIRED)
find_package(freetype CONFIG REQUIRED)
find_package(GenesisCore REQUIRED)
find_package(GLEW REQUIRED)
find_package(GLM REQUIRED)
find_package(MagicEnum REQUIRED)
find_package(rpclib REQUIRED)
find_package(SDL2 REQUIRED)
find_package(SDL2_image REQUIRED)
find_package(VPX REQUIRED)

find_path(SYSTEM_INCLUDE_DIR zlib.h)
message(STATUS "vcpkg include directory: ${SYSTEM_INCLUDE_DIR}")
include_directories(
    src/
    src/imgui/
    tools/modelcomp/src/
    ${BULLET3_INCLUDE_DIRS}
    ${CMDPARSER_INCLUDE_DIRS}
    ${GENESISCORE_INCLUDE_DIRS}
    ${GLEW_INCLUDE_DIRS}
    ${GLM_INCLUDE_DIRS}
    ${MAGICENUM_INCLUDE_DIRS}
    ${SDL2_INCLUDE_DIRS}
    ${SDL2_IMAGE_INCLUDE_DIRS}
    ${VPX_INCLUDE_DIRS}
	${SYSTEM_INCLUDE_DIR}
)

BuildExternalLibraries()

add_library(Genesis STATIC ${SOURCE_FILES})
set_target_properties(Genesis PROPERTIES ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/libs/Genesis/${CMAKE_HOST_SYSTEM_NAME}/${CMAKE_HOST_SYSTEM_PROCESSOR}/${CMAKE_BUILD_TYPE})

if(WIN32)
    find_package(FMOD REQUIRED)
    target_include_directories(Genesis PRIVATE ${FMOD_INCLUDE_DIRS})
    target_compile_definitions(Genesis PRIVATE UNICODE _UNICODE _HASEXCEPTIONS=0)
    target_compile_options(Genesis PUBLIC $<$<CONFIG:Final>:/Ob2 /GL>)
	set_target_properties(Genesis PROPERTIES VS_GLOBAL_VcpkgEnableManifest true)
else()
    find_package(SDL2_mixer REQUIRED)
endif()

target_compile_definitions(Genesis PRIVATE $<$<CONFIG:Final>:NDEBUG _FINAL> FORGE_PROCESS_PORT=${FORGE_PROCESS_PORT} FORGE_LISTENER_PORT=${FORGE_LISTENER_PORT})

target_compile_options(Genesis PRIVATE
     $<$<OR:$<CXX_COMPILER_ID:Clang>,$<CXX_COMPILER_ID:GNU>>:
          -Wall 
          -Werror
          -Wno-reorder-ctor
          -fstandalone-debug
          >
     $<$<CXX_COMPILER_ID:MSVC>:
          /MP # build with multiple processes
          /WX # warnings as errors
          /wd4100 # unreferenced formal parameter
          /wd4121 # alignment of a member was sensitive to packing
          /wd4127 # conditional expression is constant
          /wd4505 # unreferenced local function has been removed
          /wd4189 # local variable is initialized but not referenced
          /wd4201 # nonstandard extension used : nameless struct/union
          /wd4702 # unreachable code
          >
)

target_compile_definitions(Genesis PRIVATE $<$<CONFIG:Debug>:OPENGL_ERROR_CHECKING=1> $<$<CONFIG:Release>:OPENGL_ERROR_CHECKING=1> $<$<CONFIG:Final>:OPENGL_ERROR_CHECKING=0>)
add_dependencies(Genesis MagicEnum xxhash)